rootProject.name = 'java-learning'

def projectDirs = fileTree(rootDir) {
    include '**/gradle.properties'
    exclude 'gradle.properties' // Bỏ qua file properties của root project
    exclude '**/build'          // Bỏ qua thư mục build
//    include '**/build.gradle'
//    exclude '**/build.gradle.kts' // Loại trừ nếu không dùng Kotlin DSL
}.files
        .findAll {file ->
            def props = new Properties()
            file.withInputStream { props.load(it) }

            return props.getProperty('isModule') == 'true'
        }
        .collect { it.parentFile }


// Loại bỏ các thư mục trùng lặp (nếu có) và thêm vào project
projectDirs.unique().each { dir ->
    def projectPath = (dir.absolutePath + File.separator)
            .replace(rootDir.absolutePath + File.separator, '')
            .replace(File.separator, ':').toString()

    if (projectPath.length() > 0) {
        include ":${projectPath.substring(0,projectPath.length() - 1)}"
    }

}
