import groovy.json.JsonOutput
import groovy.json.JsonSlurper

plugins {
    id 'org.springframework.boot' version '3.3.0'
    id 'io.spring.dependency-management' version '1.1.5'
    id 'java'
    id 'io.freefair.lombok' version '8.6'
    id 'com.avast.gradle.docker-compose' version '0.17.6'
}

apply from: "gradle/task/common.gradle"
apply from: "gradle/config/module.gradle"
apply from: "gradle/config/properties.gradle"

List<HashMap> dirListProps = gradle.extensions.extraProperties.get("allFolderProps") as List<HashMap>

///////////////////////////////////
// task.gradle inject module
//////////////////////////////////
ext.injectTask()

///////////////////////////////////
// application.yml automation
//////////////////////////////////
List<HashMap> appProps = ext.getModulePropByKeyValue("BUILD_PROPERTIES","true",dirListProps)

List<HashMap> profileProps = ext.getModulePropByKeyValue("BUILD_PROFILE_PROPERTIES","true",appProps)
ext.buildProfilePropsFile(profileProps)
//////////////////////////////////

allprojects {
    group = 'com.example.learning'
    version = '1.0.0'

    sourceCompatibility = '17'
    targetCompatibility = '17'

    repositories {
        mavenLocal()
        mavenCentral()
        gradlePluginPortal()
    }

    project.ext.implementProjectByServiceName = rootProject.ext.findSubProjectByServiceName


}

def currentProjectKeys = [:]
gradle.extensions.extraProperties.set("allModuleKeys",currentProjectKeys)

subprojects {
    if (file("${projectDir}/gradle.properties").exists()) {
        apply plugin: 'java'
        apply plugin: 'org.springframework.boot'
        apply plugin: 'io.spring.dependency-management'
        apply plugin: "io.freefair.lombok"

        def propFile = file("${projectDir}/gradle.properties")
        Properties props = new Properties()
        propFile.withInputStream { props.load(it) }

        // Xác định Tên Module & Đường dẫn tương đối
        def moduleName = props.getProperty("SERVICE_NAME") ?: projectDir.name
        def relativePath = rootDir
                .toPath()
                .relativize(projectDir.toPath()).toString()
                .replace('\\', '/')

        props.keySet().each { key ->
            def keyStr = key.toString()
            if (!currentProjectKeys.containsKey(keyStr)) {
                currentProjectKeys[keyStr] = [:]
            }
            // Lưu Map module -> path
            currentProjectKeys[keyStr][moduleName] = relativePath
        }

//        println tasks



        def targetFile = file("${project.projectDir}/task.gradle")
        if (targetFile.exists()) {
            apply from: targetFile
        }

        processResources{
            // 1. Chỉ định rõ ràng duy nhất file này được phép copy
            include 'application.yml'

            // 2. (Tùy chọn) Nếu bạn muốn chắc chắn loại bỏ tất cả các file có đuôi .yml khác và .properties
            // Dòng này thực ra không cần thiết nếu đã dùng 'include' ở trên,
            // nhưng thêm vào sẽ giúp logic chặt chẽ hơn.
            exclude '**/application-*.yml'
            exclude '**/*.properties'

            // 3. Đặt chiến lược xử lý nếu trùng lặp (thường dùng trong Gradle mới)
            duplicatesStrategy = DuplicatesStrategy.INCLUDE
        }

        // Thay đổi đường dẫn build cho tất cả module con
        def projectPathAsFolder = project.path.replace(':', '/')
        layout.buildDirectory = rootProject.layout.buildDirectory.dir("outputs/${projectPathAsFolder}")
    }

    if (file("${projectDir}/docker-compose.yml").exists()) {
        apply plugin: 'com.avast.gradle.docker-compose'
    }

    if (file("${projectDir}/src").exists()) {
        dependencies {
            implementation 'org.springframework.boot:spring-boot-starter-web'
            implementation 'org.springframework.boot:spring-boot-starter-actuator'
            implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.3.0'
            testImplementation 'org.springframework.boot:spring-boot-starter-test'
            developmentOnly 'org.springframework.boot:spring-boot-devtools'
        }
    } else {
        [bootJar, jar].each { it.enabled = false }
    }
}

// Get all key in gradle.properties at all module and save it in json file
ext.getAllModuleProperties(currentProjectKeys)

task runAll {
    dependsOn ":application:bootRun" , ":application2:bootRun"
}




