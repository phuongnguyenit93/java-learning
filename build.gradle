plugins {
    id 'org.springframework.boot' version '3.3.0'
    id 'io.spring.dependency-management' version '1.1.5'
    id 'java'
    id 'io.freefair.lombok' version '8.6'
    id 'com.avast.gradle.docker-compose' version '0.17.6'
}

apply from: "gradle/task/common.gradle"
apply from: "gradle/config/module.gradle"
apply from: "gradle/config/properties.gradle"
apply from: "gradle/config/env.gradle"
apply from: "gradle/config/readme.gradle"

List<HashMap> dirListProps = gradle.extensions.extraProperties.get("allModuleProps") as List<HashMap>
HashMap currentProjectKeys = gradle.extensions.extraProperties.get("allModuleKeys") as HashMap

///////////////////////////////////
// task.gradle inject module
//////////////////////////////////
ext.injectTask()

///////////////////////////////////
// application.yml automation
//////////////////////////////////
List<HashMap> appProps = ext.getModulePropByKeyValue("BUILD_YML","TRUE",dirListProps)

List<HashMap> profileProps = ext.getModulePropByKeyValue("BUILD_YML_PROFILE","TRUE",appProps)
ext.buildProfilePropsFile(profileProps)
//////////////////////////////////


///////////////////////////////////
// README.md automation
//////////////////////////////////
List<HashMap> readmeProps = ext.getModulePropByKeyValue("BUILD_README","TRUE",dirListProps)
ext.buildReadmeEmptyFile(readmeProps)
ext.buildReadmeLanguageStructure(readmeProps)
///////////////////////////////////

///////////////////////////////////
// .env automation
//////////////////////////////////
List<HashMap> appEnv = ext.getModulePropByKeyValue("BUILD_ENV","TRUE",dirListProps)
ext.buildDefaultEnvStructure(appEnv)

List<HashMap> profileEnv = ext.getModulePropByKeyValue("BUILD_ENV_PROFILE","TRUE",appEnv)
ext.buildProfileEnvStructure(profileEnv)
///////////////////////////////////

///////////////////////////////////
// Get all key in gradle.properties at all module and save it in json file
///////////////////////////////////
ext.getAllModuleProperties(currentProjectKeys)

allprojects {
    group = 'com.example.learning'
    version = '1.0.0'

    sourceCompatibility = '17'
    targetCompatibility = '17'

    repositories {
        mavenLocal()
        mavenCentral()
        gradlePluginPortal()
    }

    project.ext.implementProjectByServiceName = rootProject.ext.findSubProjectByServiceName
}

subprojects {
    if (file("${projectDir}/gradle.properties").exists()) {
        apply plugin: 'java'
        apply plugin: 'org.springframework.boot'
        apply plugin: 'io.spring.dependency-management'
        apply plugin: "io.freefair.lombok"

//        println tasks

        def targetFile = file("${project.projectDir}/task.gradle")
        if (targetFile.exists()) {
            apply from: targetFile
        }
        
        processResources{
            // 1. Chỉ định rõ ràng duy nhất file này được phép copy
            include 'application.yml'

            from ("${projectDir}/src/main/resources") {
                // 2. (Tùy chọn) Nếu bạn muốn chắc chắn loại bỏ tất cả các file có đuôi .yml khác và .properties
                // Dòng này thực ra không cần thiết nếu đã dùng 'include' ở trên,
                // nhưng thêm vào sẽ giúp logic chặt chẽ hơn.
                exclude '**/application-*.yml'
                exclude '**/*.properties'
//                exclude 'application.yml'
            }

//            from("${projectDir}/src/main/resources/application-merged.yml") {
//                rename { 'application.yml' }
//            }

            exclude 'application-merged.yml'
            // 3. Đặt chiến lược xử lý nếu trùng lặp (thường dùng trong Gradle mới)
            duplicatesStrategy = DuplicatesStrategy.INCLUDE
        }

        tasks.named('bootJar') {
            // Gom vào thư mục outputs ở root, phân theo tên project
            def projectPathAsFolder = project.path.replace(':', '/')
            destinationDirectory = rootProject.layout.buildDirectory.dir("jar/${projectPathAsFolder}")
        }
    }

    if (file("${projectDir}/docker-compose.yml").exists()) {
        apply plugin: 'com.avast.gradle.docker-compose'

        dockerCompose {
            // Đọc giá trị từ gradle.properties hoặc command line (-PDOCKER_PROFILE=...)
            def profile = project.hasProperty('DOCKER_PROFILE') ? project.property('DOCKER_PROFILE') : ''

            // Nếu profile không trống, đẩy nó vào biến môi trường của Docker Compose
            if (profile) {
                environment.put 'SPRING_PROFILES_ACTIVE', profile
            }
        }
    }

    if (file("${projectDir}/src").exists()) {
        dependencies {
            implementation 'org.springframework.boot:spring-boot-starter-web'
            implementation 'org.springframework.boot:spring-boot-starter-actuator'
            implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.3.0'
            testImplementation 'org.springframework.boot:spring-boot-starter-test'
            developmentOnly 'org.springframework.boot:spring-boot-devtools'
        }
    } else {
        [bootJar, jar].each { it.enabled = false }
    }
}

task runAll {
    dependsOn ":application:bootRun" , ":application2:bootRun"
}




