plugins {
    id 'org.springframework.boot' version '3.3.0'
    id 'io.spring.dependency-management' version '1.1.5'
    id 'java'
    id 'io.freefair.lombok' version '8.6'
}

apply from: "gradle/task/inject.gradle"
apply from: "gradle/config/modules.gradle"
apply from: "gradle/config/properties.gradle"

List<HashMap> dirListProps = ext.getFolderProps()

///////////////////////////////////
// application.yml automation
//////////////////////////////////
List<HashMap> appProps = ext.getModulePropByKeyValue("BUILD_PROPERTIES","true",dirListProps)

List<HashMap> profileProps = ext.getModulePropByKeyValue("BUILD_PROFILE_PROPERTIES","true",appProps)
ext.buildProfilePropsFile(profileProps)


//////////////////////////////////
[bootJar, jar].each { it.enabled = false }

allprojects {
    group = 'com.example.learning'
    version = '1.0.0'

    sourceCompatibility = '17'
    targetCompatibility = '17'

    repositories {
        mavenLocal()
        mavenCentral()
        gradlePluginPortal()
    }

    project.ext.implementProjectByServiceName = rootProject.ext.findSubProjectByServiceName
}

subprojects {
    if (file("${projectDir}/gradle.properties").exists()) {
        apply plugin: 'java'
        apply plugin: 'org.springframework.boot'
        apply plugin: 'io.spring.dependency-management'
        apply plugin: "io.freefair.lombok"

        println tasks

        // Thay đổi đường dẫn build cho tất cả module con
        def projectPathAsFolder = project.path.replace(':', '/')
        layout.buildDirectory = rootProject.layout.buildDirectory.dir("outputs/${projectPathAsFolder}")
    }

    if (file("${projectDir}/src").exists()) {
        dependencies {
            implementation 'org.springframework.boot:spring-boot-starter-web'
            implementation 'org.springframework.boot:spring-boot-starter-actuator'
            implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.3.0'
            testImplementation 'org.springframework.boot:spring-boot-starter-test'
            developmentOnly 'org.springframework.boot:spring-boot-devtools'
        }

        def targetFile = file("${project.projectDir}/task.gradle")
        if (targetFile.exists()) {
            apply from: targetFile
        }
    } else {
        [bootJar, jar].each { it.enabled = false }
    }

}

task runAll {
    dependsOn ":application:bootRun" , ":application2:bootRun"
}




