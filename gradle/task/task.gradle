apply from: "gradle/config/readme.gradle"

abstract class BuildDocumentByServiceName extends DefaultTask {
    @Input
    abstract Property<String> getServiceName()

    @Input
    abstract ListProperty<HashMap> getFolderPropsList()

    @TaskAction
    void executeTask() {
        List<HashMap> folderProps = folderPropsList.get() as List<HashMap>
        String service = serviceName.get()

        HashMap props = folderProps
                .find{props -> props.SERVICE_NAME == service && props.BUILD_README == 'true'}

        if (props == null) return

        def modulePath = project.project(":${props.get("MODULE_PATH")}")
        def languageList = props.get("README_LANGUAGE")?.toString()?.split(",")

        languageList.each {lang ->
            project.ext.generateMdList(modulePath.projectDir,lang)
            project.ext.combineMarkdown(modulePath.projectDir,lang)
        }
    }
}

tasks.register('buildDocumentByServiceName', BuildDocumentByServiceName) {
    serviceName = project.findProperty("serviceName")
    folderPropsList = gradle.extensions.getExtraProperties().get("allFolderProps")
}



