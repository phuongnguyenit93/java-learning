ext.buildProfileEnvStructure = {List<HashMap> envProps ->
    envProps.each {props ->
        File baseDir = props.get("ABSOLUTE_PATH") as File

        List<String> profiles = gradle.extensions.extraProperties.get("profiles") as List<String>

        profiles.each {profile ->
            File file = new File(baseDir, ".env${!profile.equals("default") ? ".${profile}" : ""}")
            if (!file.exists()) file.createNewFile()
        }

    }
}

ext.buildModuleEnvProjectStructure = {List<HashMap> moduleEnv ->
    moduleEnv.each {props ->
        String basePath = props.get("ABSOLUTE_PATH")

        List<String> profiles = gradle.extensions.extraProperties.get("profiles") as List<String>

        profiles.each {profile ->
            File profileDir = new File(basePath as File,"config/${profile}/env")
            if (!profileDir.exists()) profileDir.mkdirs()

            [".env.base",".env.module"].each {fileName ->
                File file = new File(profileDir, fileName)
                if (!file.exists()) file.createNewFile()
            }
        }
    }
}

ext.buildModuleEnvPathStructure = {List<HashMap> modulePropsList,List<HashMap> dirListProps ->
    modulePropsList.each {prop ->
        boolean isDockerBuild = ['BUILD_ENV', 'BUILD_ENV_PATH'].every { prop[it] == 'true' }

        if (isDockerBuild) {

            List<String> microserviceList = prop.get("MODULE_ENV_PATH_LIST")?.toString()?.split(',') as List<String> ?: []

            String moduleName = prop.get("SERVICE_NAME")
            if (moduleName != null || !moduleName.equals("")) microserviceList.add(moduleName)

            List<HashMap> filterMicroserviceProps = ext.getModulePropByServiceNameList(microserviceList,dirListProps)

            StringBuilder content = new StringBuilder("")

            content.append("PROJECT_ROOT=${rootDir.toString().replace("\\","/")}\n\n")
            filterMicroserviceProps.each {service ->
                content.append("# --- SERVICE_NAME: ${service.get("SERVICE_NAME")} ---\n")
                content.append("${service.get("SERVICE_NAME")}_ABSOLUTE_PATH=${service.get("ABSOLUTE_PATH")}\n")
                content.append("${service.get("SERVICE_NAME")}_JAR_PATH=${service.get("JAR_PATH")}\n")
                content.append("${service.get("SERVICE_NAME")}_MODULE_PATH=${service.get("MODULE_PATH")}\n")
                content.append("\n\n")
            }

            def targetEnvFile = new File(prop.get("ABSOLUTE_PATH") as File, ".env.module.path")
            if (!targetEnvFile.exists()) targetEnvFile.createNewFile()
            targetEnvFile.text = content.toString()
        }
    }
}

ext.combineEnvAuto = {List<HashMap> dockerPropsList ->
    dockerPropsList.each {prop ->
        boolean isDockerCombine = ['BUILD_ENV', 'FINAL_ENV_AUTO'].every { prop[it] == 'true' }

        if (isDockerCombine) {
            def dockerEnv = new File(prop.get("ABSOLUTE_PATH") as File, ".env.module")
            def baseEnv = new File(prop.get("ABSOLUTE_PATH") as File, ".env.base")
            def env = new File(prop.get("ABSOLUTE_PATH") as File, ".env")
            def exampleEnv = new File(prop.get("ABSOLUTE_PATH") as File, ".env.example")

            if (!env.exists()) env.createNewFile()
            if (!exampleEnv.exists()) exampleEnv.createNewFile()

            if (baseEnv.exists() && dockerEnv.exists()) {

                // ENV file
                String combinedContent = baseEnv.text + "\n" + dockerEnv.text
                env.text = combinedContent

                // ENV example file
                def props = new Properties()
                baseEnv.withInputStream { props.load(it) }
                dockerEnv.withInputStream { props.load(it) }

                def exampleContent = new StringBuilder()
                exampleContent.append("# Example Env - Do not input any value in here\n\n")

                props.keySet().sort().each { key ->
                    exampleContent.append("${key}=your_value_here\n")
                }

                exampleEnv.text = exampleContent.toString()

            } else {
                println "‚ùå Missing .env.base or .env.docker"
            }

        }
    }
}


ext.combineEnvByServiceNameAndProfile = {ListProperty<HashMap> folderPropsList,Property<String> serviceNameProps,Property<String> profileProps ->
    String profile = profileProps.getOrElse("default")
    String profileName
    String serviceName = serviceNameProps.get();
    if (profile != "default") {
        List<String> profileList = gradle.extensions.extraProperties.get("profiles")

        if (!profileList.contains(profile)) {
            throw new GradleException("Your Profile value not in out profile list: ${profileList}. Please check again")
        } else {
            profileName = ".env.${profile}"
        }
    } else {
        profileName = ".env"
    }

    HashMap propFound = folderPropsList.get()
            .find{props -> props.SERVICE_NAME == serviceName && props.FINAL_ENV_AUTO == 'true'}

    File basePath = propFound.get("ABSOLUTE_PATH") as File
    File combineFile = new File(basePath, profileName)

    StringBuilder content = new StringBuilder("# Env generated Auto \n")
    [".env.base",".env.module",".env.module.path"].each {file ->
        File path = new File (basePath,"${file != ".env.module.path" ? "config/${profile}/env/${file}" : file}")

        content.append(path.text)
        content.append("\n\n")
    }

    combineFile.text = content
}
